/*
  Copyright (C) 2016-2017 Petr Vytovtov
  Contact: Petr Vytovtov <osanwe@protonmail.ch>
  All rights reserved.

  This file is part of Kat.

  Kat is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  Kat is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with Kat.  If not, see <http://www.gnu.org/licenses/>.
*/

import QtQuick 2.0
import Sailfish.Silica 1.0

Page {
    id: imageViewPage

    property var ownerIds
    property var photoIds
    property var imagesModel
    property var current

    Drawer {
        id: drawer
        anchors.fill: parent

        open: true

        background: SilicaListView {
            anchors.fill: parent

            model: ListModel {

                ListElement { title: "Save" }
                ListElement { title: "Like" }
                ListElement { title: "Share" }
                ListElement { title: "Copy link" }
            }

            header: PageHeader {
                title: (slideshowView.currentIndex + 1) + ' / ' + imagesModel.length
            }

            delegate: ListItem {

                width: parent.width
                height: Theme.itemSizeSmall

                Label {
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.verticalCenter: parent.verticalCenter
                    width: parent.width - 2 * Theme.horizontalPageMargin
                    text: model.title
                }

                onClicked: switch (index) {
                           case 0:
                               fileSaver.save(imagesModel[slideshowView.currentIndex])
                               break;
                           case 1:
                               vksdk.likes.addPhoto(ownerIds[slideshowView.currentIndex],
                                                    photoIds[slideshowView.currentIndex])
                               break;
                           case 2:
                               pageStack.push(Qt.resolvedUrl("RepostPage.qml"), { sourceId: ownerIds[slideshowView.currentIndex],
                                                                                  postId: photoIds[slideshowView.currentIndex],
                                                                                  type: "photo" })
                               break;
                           case 3:
                               Clipboard.text = imagesModel[slideshowView.currentIndex]
                               break;
                           }
            }
        }

        SlideshowView {
            id: slideshowView
            width: imageViewPage.width
            height: imageViewPage.height
            model: imagesModel.length

            delegate: Image {
                    id: imageView
                    width: imageViewPage.width
                    height: width * (sourceSize.height / sourceSize.width)
                    fillMode: Image.PreserveAspectFit
                    source: imagesModel[index]

                    PinchArea {
                        anchors.fill: parent
                        pinch.target: parent
                        pinch.minimumScale: 1
                        pinch.maximumScale: 4

                        MouseArea {
                            anchors.fill: parent
                            drag.target: imageView
                            drag.axis: Drag.XAndYAxis
                            drag.minimumX: (slideshowView.width - imageView.width * imageView.scale) / 2
                            drag.minimumY: (slideshowView.height - imageView.height * imageView.scale) / 2
                            drag.maximumX: Math.abs(slideshowView.width - imageView.width * imageView.scale) / 2
                            drag.maximumY: Math.abs(slideshowView.height - imageView.height * imageView.scale) / 2
                            onClicked: drawer.open = !drawer.open
                            onPositionChanged: { // TODO
                                console.log(drag.minimumX + " <= " + imageView.x + " <= " + drag.maximumX)
                                console.log(drag.minimumY + " <= " + imageView.y + " <= " + drag.maximumY)
                            }
                        }
                    }
                }
        }
    }

    Component.onCompleted: slideshowView.currentIndex = current
}

